# Maintainer: Gabriel Peixoto <gabrielrcp@gmail.com>

pkgname=manga_downloader-git
pkgver=20130319
pkgrel=1
pkgdesc="Python script for downloading manga from varios websites"
arch=('any')
url="https://github.com/jiaweihli/manga_downloader"
license=('GPL3')
depends=('python2')
optdepends=('python2-imaging: for kindle conversion')
makedepends=('git')

provides=('manga_downloader')
conflicts=('manga_downloader')

_gitroot=git://github.com/jiaweihli/manga_downloader.git
_gitname=manga_downloader


source=('start-location.patch')

md5sums=('782db7c06c64926b5288ed6e2c864c2a')

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"



  # stop the script from trying to save manga in the install folder
  patch -Np1 -i "$srcdir/start-location.patch"

  # python fix
  find -type f -exec sed -e 's,#!/usr/bin/env python,#!/usr/bin/env python2,g' -i {} \;
}


package() {
  cd "$srcdir/$_gitname-build/src"

  install -dm 755 "$pkgdir/usr/share/manga_downloader"
  cp -r . "$pkgdir/usr/share/manga_downloader"
  install -dm 755 "$pkgdir/usr/bin/"
  ln -s /usr/share/manga_downloader/manga.py "$pkgdir/usr/bin/manga_downloader"
}
